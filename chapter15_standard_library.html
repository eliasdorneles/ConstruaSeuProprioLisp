<h1>Biblioteca Padrão <small>&bull; Capítulo 15</small></h1>

<h2>Minimalismo</h2> <hr/>

<div class='pull-right alert alert-warning' style="margin: 15px; text-align: center;">
  <img src="/static/img/library.png" alt="library" class="img-responsive" width="299px" height="444px"/>
  <p><small>Biblioteca &bull; Construída com apenas couro, papel, madeira e tinta.</small></p>
</div>

<p>O Lisp que construímos é propositalmente mínimo. Adicionamos um número pequeno de estruturas e builtins. Quando escolhemos essas coisas com cuidado, como fizemos, deve nos permitir adicionar tudo o mais necessário à linguagem.</p>

<p>A motivação para o minimalismo é dupla. A primeira vantagem é que torna o núcleo da linguagem simples de depurar e fácil de aprender. Isso é um grande benefício para desenvolvedores e usuários. Como a <a href="https://pt.wikipedia.org/wiki/Navalha_de_Occam">Navalha de Occam</a>, é quase sempre melhor remover qualquer desperdício se resultar numa linguagem igualmente expressiva. A segunda razão é que ter uma linguagem pequena é esteticamente mais legal. É inteligente, interessante e divertido ver quão pequeno conseguimos fazer o núcleo da linguagem, e ainda ter algo útil do outro lado. Como <em>hackers</em>, que devemos ser por ora, isto é algo que curtimos.</p>


<h2>Átomos</h2> <hr/>

<p>Ao lidar com condicionais, não adicionamos nenhum tipo booleano à nossa linguagem. Por causa disso, também não adicionamos <code>true</code> ou <code>false</code>. Ao invés disso, simplesmente usamos números. Legibilidade é todavia ainda importante, então podemos definir algumas constantes para representar estes valores.</p>

<p>Numa observação parecida, muitos lisps usam a palavra <code>nil</code> para representar a lista vazia <code>{}</code>. Podemos adicionar isso também. Estas constantes são às vezes chamadas de <em>átomos</em> porque elas são fundamentais e constantes.</p>

<p>O usuário não é forçado a usar estas constantes nomeadas, e podem usar números e listas vazias no lugar, como quiserem. Esta escolha empodera usuários, algo em que nós acreditamos.</p>


<pre><code data-language='lispy'>; Atomos
(def {nil} {})
(def {true} 1)
(def {false} 0)</code></pre>


<h2>Blocos de Construção</h2> <hr/>

<p>Já inventamos uma boa quantidade de funções legais que usamos nos exemplos. Uma destas é nossa função <code>fun</code> que nos permite declarar funções de maneira mais clara. Devemos definitivamente incluí-la em nossa biblioteca padrão.</p>

<pre><code data-language='lispy'>; Definicoes de funcoes
(def {fun} (\ {f b} {
  def (head f) (\ (tail f) b)
}))</code></pre>

<p>Também tivemos nossas funções <code>unpack</code> e <code>pack</code>. Estas também serão essenciais para usuários. Devemos incluí-las, juntamente com seus pseudônimos <code>curry</code> e <code>uncurry</code>.</p>


<pre><code data-language='lispy'>; Desempacota lista para funcao
(fun {unpack f l} {
  eval (join (list f) l)
})

; Empacota lista para funcao
(fun {pack f &amp; xs} {f xs})

; Curried and Uncurried calling
(def {curry} unpack)
(def {uncurry} pack)</code></pre>

<p>Digamos que queiramos fazer várias coisas em ordem. Uma maneira de fazer isto é colocar cada coisa a fazer como argumento à uma função. Sabemos que argumentos são avaliados em ordem da esquerda para direita, o que é essencialmente sequenciar eventos. Para funções como <code>print</code> e <code>load</code> não nos importamos muito com o que elas avaliam, mas nos importamos com a ordem em que isso acontece.</p>

<p>Por isso, podemos criar uma função <code>do</code> que avalia algumas expressões em ordem e retorna a última. Ela se fia na função <code>last</code> que retorna o último elemento da lista. Vamos defini-la depois.</p>

<pre><code data-language='lispy'>; Executa varias coisas em sequencia
(fun {do &amp; l} {
  if (== l nil)
    {nil}
    {last l}
})</code></pre>

<p>Às vezes queremos guardar os resultados em variáveis locais usando o operador <code>=</code>. Quanto estamos dentro de uma função, isto vai implicitamente salvar resultados apenas localmente, mas às vezes queremos abrir um escopo ainda mais local. Para isto, podemos criar uma unção <code>let</code> que cria uma função vazia para código acontecer dentro, e avaliá-lo.</p>

<pre><code data-language='lispy'>; Abre novo escopo
(fun {let b} {
  ((\ {_} b) ())
})</code></pre>

<p>Podemos usar isto em conjunto com <code>do</code> para nos certificar que variáveis não vazam para fora de seu escopo.</p>

<pre><code data-language='lispy'>lispy&gt; let {do (= {x} 100) (x)}
100
lispy&gt; x
Error: Unbound Symbol 'x'
lispy&gt;</code></pre>


<h2>Operadores Lógicos</h2> <hr/>

<p>Não definimos nenhum operadores locais como <code>and</code> e <code>ou</code> em nossa linguagem. Esta pode ser uma boa coisa para adicionar depois. Por enquanto, podemos usar operadores aritméticos para emulá-los. Pense em como essas funções vão funcionar quando encontrar <code>0</code> ou <code>1</code> para suas diferentes entradas.</p>

<pre><code data-language='lispy'>; Funcoes Logicas
(fun {not x}   {- 1 x})
(fun {or x y}  {+ x y})
(fun {and x y} {* x y})</code></pre>


<h2>Funções Variadas</h2> <hr/>

<p>Aqui estão algumas outras funções variadas que não se encaixam mesmo em qualquer lugar. Veja se você consegue adivinhar sua funcionalidade pretendida.</p>

<pre><code data-language='lispy'>(fun {flip f a b} {f b a})
(fun {ghost &amp; xs} {eval xs})
(fun {comp f g x} {f (g x)})</code></pre>

<p>A função <code>flip</code> recebe uma função <code>f</code> e dois argumentos <code>a</code> e <code>b</code>. Ela então aplica <code>f</code> à <code>a</code> e <code>b</code> na ordem invertida. Isto pode ser útil quando queremos que uma função seja <em>parcialmente avaliada</em>. Se queremos avaliar parcialmente uma função apenas passando-a em seu segundo argumento, podemos usar <code>flip</code> para nos dar uma nova função que receba os primeiros dois argumentos em ordem invertida.</p>

<p>Isto significa que caso queira aplicar o segundo argumento de uma função, você pode simplesmente aplicar o primeiro argumento ao <code>flip</code> desta função.</p>

<pre><code data-language='lispy'>lispy&gt; (flip def) 1 {x}
()
lispy&gt; x
1
lispy&gt; def {define-one} ((flip def) 1)
()
lispy&gt; define-one {y}
()
lispy&gt; y
1
lispy>
</code></pre>

<p>Não consegui pensar em nenhum uso para a função <code>ghost</code>, mas pareceu interessante. Ela simplesmente recebe um número qualquer de argumentos e os avalia como se fossem a própria expressão. Então ela fica na frente de uma expressão como um fantasma, não interagindo com ou mudando o comportamento do programa em nada. Se você puder pensar em algum uso para ela, eu adoraria ouvir.</p>

<pre><code data-language='lispy'>lispy&gt; ghost + 2 2
4
</code></pre>

<p>A função <code>comp</code> é usara para <em>compor</em> duas funções. Ela recebe como entrada <code>f</code>, <code>g</code> e um argumento para <code>g</code>. Ela então aplica este argumento à <code>g</code> e aplica o resultado novamente à <code>f</code>. Isto pode ser usado para compor funções em uma nova função que aplica ambas em série. Como antes, podemos usar isto em combinação com avaliação parcial para construir funções complexas a partir de outras mais simples.</p>

<p>Por exemplo, podemos compor duas funções. Uma nega um número e outra desempacota uma lista de números para os multiplicar com <code>*</code>.</p>

<pre><code data-language='lispy'>lispy&gt; (unpack *) {2 2}
4
lispy&gt; - ((unpack *) {2 2})
-4
lispy&gt; comp - (unpack *)
(\ {x} {f (g x)})
lispy&gt; def {mul-neg} (comp - (unpack *))
()
lispy&gt; mul-neg {2 8}
-16
lispy&gt;
</code></pre>


<h2>Funções de Lista</h2> <hr/>

<p>A função <code>head</code> é usada para obter o primeiro elemento duma lista, mas o que ela retorna ainda está envolto na lista. Caso queiramos pegar o elemento fora desta lista, precisamos extraí-lo de alguma maneira.</p>

<p>Listas com apenas um elemento avaliam para apenas aquele elemento, então podemos usar a função <code>eval</code> para fazer esta extração. Podemos também definir algumas funções utilitárias para ajudar a extrair o primeiro, segundo ou terceiro elemento de uma lista. Vamos usar mais estas funções mais tarde.</p>

<pre><code data-language='lispy'>; First, Second, or Third Item in List
(fun {fst l} { eval (head l) })
(fun {snd l} { eval (head (tail l)) })
(fun {trd l} { eval (head (tail (tail l))) })</code></pre>

<p>Olhamos brevemente em algumas funções recursivas de listas alguns capítulos atrás. Naturalmente existem muitas mais que podemos definir usando esta técnica.</p>

<p>Para encontrar o tamanho de uma lista podemos recorrer sobre ela adicionado 1 ao comprimento da cauda. Para encontrar o enésimo elemento de uma lista podemos executar a operação <code>tail</code> e contar para baixo até atingirmos <code>0</code>. Para obter o último elemento de uma lista podemos simplesmente acessar o elemento do comprimento menos um.</p>

<pre><code data-language='lispy'>; Comprimento da lista
(fun {len l} {
  if (== l nil)
    {0}
    {+ 1 (len (tail l))}
})</code></pre>

<pre><code data-language='lispy'>; Nth - enesimo elemento
(fun {nth n l} {
  if (== n 0)
    {fst l}
    {nth (- n 1) (tail l)}
})</code></pre>

<pre><code data-language='lispy'>; Ultimo elemento
(fun {last l} {nth (- (len l) 1) l})</code></pre>

<p>Há muitas outras funções úteis que seguem este mesmo padrão. Podemos definir funções para tomar ou descartar os primeiros tantos elementos de uma lista, ou funções para checar se um valor é membro de uma lista.</p>

<pre><code data-language='lispy'>; Tomar N itens
(fun {take n l} {
  if (== n 0)
    {nil}
    {join (head l) (take (- n 1) (tail l))}
})</code></pre>

<pre><code data-language='lispy'>; Descartar N itens
(fun {drop n l} {
  if (== n 0)
    {l}
    {drop (- n 1) (tail l)}
})</code></pre>

<pre><code data-language='lispy'>; Split at N
(fun {split n l} {list (take n l) (drop n l)})</code></pre>

<pre><code data-language='lispy'>; Element of List
(fun {elem x l} {
  if (== l nil)
    {false}
    {if (== x (fst l)) {true} {elem x (tail l)}}
})
</code></pre>

<p>Todas estas funções seguem padrões similares. Seria ótimo se houvesse uma maneira de extrair este padrão para que não tenhamos que digitá-lo toda vez. Por exemplo, podemos querer uma maneira de executar uma função em cada elemento duma lista. Esta função podemos definir chamando de <code>map</code>. Ela recebe como entrada uma função e uma lista. Para cada item na lista ela aplica a função para aquele item e o acrescenta na frente da lista. A seguir ela aplica <code>map</code> à cauda da lista.</p>

<pre><code data-language='lispy'>; Apply Function to List
(fun {map f l} {
  if (== l nil)
    {nil}
    {join (list (f (fst l))) (map f (tail l))}
})</code></pre>

<p>Com isto podemos fazer algumas coisas legais que se parecem com laços. Em algumas maneiras, este conceito é mais poderoso que laços. Em vez de pensar sobre executar alguma função em cada elemento da lista, podemos pensar sobre agir em todos elementos de uma vez só. Nós <em>mapeamos a lista</em> em vez de <em>mudar cada elemento</em>.</p>

<pre><code data-language='lispy'>lispy&gt; map - {5 6 7 8 2 22 44}
{-5 -6 -7 -8 -2 -22 -44}
lispy&gt; map (\ {x} {+ x 10}) {5 2 11}
{15 12 21}
lispy&gt; print {"hello" "world"}
{"hello" "world"}
()
lispy&gt; map print {"hello" "world"}
"hello"
"world"
{() ()}
lispy&gt;</code></pre>

<p>Uma adaptação desta ideia é uma função <code>filter</code> que recebe uma função condicional e somente inclui itens de uma lista que correspondam àquela condição.</p>

<pre><code data-language='lispy'>; Aplica Filtro a uma Lista
(fun {filter f l} {
  if (== l nil)
    {nil}
    {join (if (f (fst l)) {head l} {nil}) (filter f (tail l))}
})</code></pre>

<p>Assim que fica na prática:</p>

<pre><code data-language='lispy'>lispy&gt; filter (\ {x} {&gt; x 2}) {5 2 11 -7 8 1}
{5 11 8}</code></pre>

<p>Alguns laços não agem exatamente em uma lista, mas acumular algum total ou condensam a lista em um valor único. Estes são laços como somatórios e produtórios, e podem ser expressos de maneira parecida à função <code>len</code> que já definimos.</p>

<p>São chamados <em>folds</em> (<em>dobras</em>) e funcionam assim: supridos com uma função <code>f</code>, um <em>valor base</em> <code>z</code> e uma lista <code>l</code>, eles fundem cada elemento na lista com o total, começando com o valor base.</p>

<pre><code data-language='lispy'>; Fold Left (Dobra a Esquerda)
(fun {foldl f z l} {
  if (== l nil)
    {z}
    {foldl f (f z (fst l)) (tail l)}
})</code></pre>

<p>Usando folds podemos definir as funções <code>sum</code> e <code>product</code> de maneira bem elegante.</p>

<pre><code data-language='lispy'>(fun {sum l} {foldl + 0 l})
(fun {product l} {foldl * 1 l})</code></pre>


<h2>Funções Condicionais</h2> <hr/>

<p>Ao definir nossa função <code>fun</code> já mostramos quão poderosa nossa linguagem é na sua capacidade de definir funções que se parecem com sintaxe. Um outro exemplo disso é encontrado em emular os comandos C <code>switch</code> e <code>case</code>. Em C, eles são construídos na linguagem, mas para nossa linguagem podemos defini-los como parte da biblioteca.</p>

<p>Podemos definir uma função <code>select</code> que recebe zero ou mais listas de dois elementos como entrada. Para cada lista de dois elementos nos argumentos, ela primeiro avalia o primeiro argumento do par. Caso for verdadeiro, então ele avalia e retorna o segundo item, caso contrário ele executa a mesma coisa novamente no resto da lista.</p>

<pre><code data-language='lispy'>(fun {select &amp; cs} {
  if (== cs nil)
    {error "No Selection Found"}
    {if (fst (fst cs)) {snd (fst cs)} {unpack select (tail cs)}}
})</code></pre>

<p>Podemos definir uma função <code>otherwise</code> (significando, caso contrário) que sempre avalie como <code>true</code>. Isto funciona um como a palavra-chave <code>default</code> em C.</p>

<pre><code data-language='lispy'>; Caso default
(def {otherwise} true)

; Imprime sufixo do dia do mes
(fun {month-day-suffix i} {
  select
    {(== i 0)  "st"}
    {(== i 1)  "nd"}
    {(== i 3)  "rd"}
    {otherwise "th"}
})</code></pre>

<p>Isto é na verdade mais poderoso que o comando <code>switch</code> em C. Em C, no lugar de passar condições, o valor da entrada é comparado apenas para igualdade com um número de candidatos constantes. Nós também podemos definir esta função em nosso Lisp, onde comparamos um valor a um número de candidatos. Nesta função, recebemos um valor <code>x</code> seguido de zero ou mais listas de dois elementos novamente. Se o primeiro elemento na lista de dois elementos for igual a <code>x</code>, o segundo elemento é avaliado, senão o processo continua lista abaixo.</p>

<pre><code data-language='lispy'>(fun {case x &amp; cs} {
  if (== cs nil)
    {error "No Case Found"}
    {if (== x (fst (fst cs))) {snd (fst cs)} {
      unpack case (join (list x) (tail cs))}}
})</code></pre>

<p>A sintaxe para esta função se torna bem legal e simples. Tente ver se você consegue pensar em outras estruturas de controle ou funções úteis que você gostaria de implementar usando estes tipos de métodos.</p>

<pre><code data-language='lispy'>(fun {day-name x} {
  case x
    {0 "Monday"}
    {1 "Tuesday"}
    {2 "Wednesday"}
    {3 "Thursday"}
    {4 "Friday"}
    {5 "Saturday"}
    {6 "Sunday"}
})</code></pre>


<h2>Fibonacci</h2> <hr/>

<p>Nenhuma biblioteca padrão seria completa sem uma definição obrigatória da função Fibonacci. Usando todas as coisas que definimos acima, podemos escrever uma função <code>fib</code> fofinha que é realmente bem legível e semanticamente clara.</p>

<pre><code data-language='lispy'>; Fibonacci
(fun {fib n} {
  select
    { (== n 0) 0 }
    { (== n 1) 1 }
    { otherwise (+ (fib (- n 1)) (fib (- n 2))) }
})</code></pre>

<p>Este é o fim da biblioteca padrão que escrevi. Construir uma biblioteca padrão é uma parte divertida do projeto de uma linguagem, porque você pode ser criativo e determinado no que entra e no que fica fora. Tente inventar alguma coisa que você curta. Explore o que é possível definir e faça o que for interessante.</p>


<h2>Referência</h2> <hr/>

<references />

<h2>Metas Bônus</h2> <hr/>

<div class="alert alert-warning">
  <ul class="list-group">
    <li class="list-group-item">&rsaquo; Reescreva a função <code>len</code> usando <code>foldl</code>.</li>
    <li class="list-group-item">&rsaquo; Reescreva a função <code>elem</code> usando <code>foldl</code>.</li>
    <li class="list-group-item">&rsaquo; Incorpore sua biblioteca padrão diretamente dentro da linguagem. Faça carregar ao iniciar.</li>
    <li class="list-group-item">&rsaquo; Escreva alguma documentação para sua biblioteca padrão, explicando o que cada uma das funções fazem.</li>
    <li class="list-group-item">&rsaquo; Escreva alguns programas exemplos usando sua biblioteca padrão, para usuários aprenderem com eles.</li>
    <li class="list-group-item">&rsaquo; Escreva alguns casos de teste para cada uma das funções na sua biblioteca padrão.</li>
  </ul>
</div>


<h2>Navegação</h2>

<table class="table" style='table-layout: fixed;'>
  <tr>
    <td class="text-left"><a href="chapter14_strings"><h4>&lsaquo; Strings</h4></a></td>
    <td class="text-center"><a href="contents"><h4>&bull; Conteúdo &bull;</h4></a></td>
    <td class="text-right"><a href="chapter16_bonus_projects"><h4>Projetos Extras &rsaquo;</h4></a></td>
  </tr>
</table>
